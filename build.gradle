/*
 * Copyright 2019, Perfect Sense, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

def releaseVersion = '1.1.3rc3'
def releaseBuild = false;
import org.apache.tools.ant.taskdefs.condition.Os;

if (System.getenv('CI')) {
    defaultTasks 'build', 'publish'

    releaseBuild = System.getenv('GITHUB_ACTIONS_PULL_REQUEST') == '' &&
            System.getenv('GITHUB_REF').startsWith("refs/tags/release/");
} else {
    defaultTasks 'build', 'publishToMavenLocal'
}

allprojects {
    group = 'gyro'
    version = releaseBuild ? releaseVersion : releaseVersion + '-SNAPSHOT'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'checkstyle'
    apply plugin: 'maven-publish'

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenCentral()

        maven {
            url 'https://artifactory.psdops.com/public'
        }
    }

    dependencies {
        implementation 'ch.qos.logback:logback-classic:1.2.3'
        implementation 'org.slf4j:log4j-over-slf4j:1.7.16'
        implementation 'org.slf4j:slf4j-api:1.7.16'
        implementation 'com.fasterxml.jackson.core:jackson-core:2.13.1'

        testImplementation 'com.psddev:test:4.1-SNAPSHOT'
        testImplementation 'org.assertj:assertj-core:3.11.1'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.2'
        testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.3.2'
        testImplementation 'org.junit.jupiter:junit-jupiter-params:5.3.2'
        testImplementation 'org.mockito:mockito-core:2.23.4'
        testImplementation 'org.mockito:mockito-junit-jupiter:2.23.0'
    }

    checkstyle {
        toolVersion = '8.24'
        configFile = rootProject.file('config/checkstyle.xml')
    }

    test {
        useJUnitPlatform()
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }

        repositories {
            maven {
                credentials {
                    username System.getenv('GRADLE_DEPLOY_USER')
                    password System.getenv('GRADLE_DEPLOY_PASSWORD')
                }

                if (releaseBuild) {
                    url 'https://artifactory.psdops.com/gyro-releases'

                } else {
                    url 'https://artifactory.psdops.com/gyro-snapshots'
                }
            }
        }
    }
}

// Only publish JARs once during the Linux build.
configure(subprojects.findAll { it.name != 'gyro-cli' }) {
    publishMavenPublicationToMavenRepository.onlyIf { Os.isFamily(Os.FAMILY_UNIX) && !Os.isFamily(Os.FAMILY_MAC) }
}
