/*
 * Copyright 2019, Perfect Sense, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'org.springframework.boot' version '2.1.0.RELEASE'
}

dependencies {
    api project(':gyro-core')

    implementation 'com.psddev:dari-util:3.3.607-xe0f27a'
    implementation 'org.fusesource.jansi:jansi:1.16'
    implementation 'org.reflections:reflections:0.9.10'
}

bootJar {
    mainClassName = 'gyro.cli.Gyro'

    launchScript {
        script = file 'scripts/stub.sh'
    }
}

task createDistributionDir {
    mkdir 'dist'
}

task createDistributionBinary(type: Copy) {
    dependsOn bootJar
    dependsOn createDistributionDir

    from bootJar.archiveFile
    rename '.+', 'gyro'
    into 'dist'
}

def OS_NAME = System.getenv('TRAVIS_OS_NAME') ?: 'unix'
if (Os.isFamily(Os.FAMILY_WINDOWS)) {
    OS_NAME = 'windows'
}

task bundleDistribution(type: Exec) {
    dependsOn createDistributionBinary

    environment OS_NAME: "${OS_NAME}"
    environment VERSION: "${version}"
    
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine 'cmd', '/c', 'scripts\\build-gyro-dist.bat'
    } else {
        commandLine 'sh', '-c', 'scripts/build-gyro-dist.sh'
    }
}

publish.dependsOn bundleDistribution
publishToMavenLocal.dependsOn bundleDistribution

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = "gyro-cli-${OS_NAME}"

            artifact 'dist/gyro'

            artifact("dist/gyro-cli-${OS_NAME}-${version}.zip") {
                extension 'zip'
            }
        }
    }
}
