plugins {
    id 'org.springframework.boot' version '2.1.0.RELEASE'
}

dependencies {
    implementation project(':gyro-core')
    implementation 'com.psddev:dari-util:3.3.607-xe0f27a'
    implementation 'io.airlift:airline:0.8'
    implementation 'org.reflections:reflections:0.9.10'
}

bootJar {
    mainClassName = 'gyro.cli.Gyro'

    launchScript {
        script = file 'scripts/stub.sh'
    }
}

task createDistributionDir {
    mkdir 'dist'
}

task createDistributionBinary(type: Copy) {
    dependsOn bootJar
    dependsOn createDistributionDir

    from bootJar.archiveFile
    rename '.+', 'gyro'
    into 'dist'
}

def OS_NAME = System.getenv('TRAVIS_OS_NAME') ?: 'unix'

task bundleDistribution(type: Exec) {
    dependsOn createDistributionBinary

    environment OS_NAME: "${OS_NAME}"
    environment VERSION: "${version}"
    commandLine 'sh', '-c', 'scripts/build-gyro-dist.sh'
}

publish.dependsOn bundleDistribution
publishToMavenLocal.dependsOn bundleDistribution

publishing {
    publications {
        maven(MavenPublication) {
            artifact 'dist/gyro'

            artifact("dist/gyro-cli-${OS_NAME}-${version}.zip") {
                extension 'zip'
            }
        }
    }
}
